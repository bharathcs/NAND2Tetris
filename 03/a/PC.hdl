// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16 (in=regOut, out=incOut);
    Mux16 (a=regOut, b=incOut, sel=inc, out=x);
    // x is regOut incremented if inc, else it is regOut

    Mux16 (a=x, b=in, sel=load, out=y);
    // y is input if load, else it is x

    Mux16 (a=y, b=false, sel=reset, out=z);
    // z is 0 if reset, else it is y

    Register (in=z, load=true, out=regOut, out=out);
}
